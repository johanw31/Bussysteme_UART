#include <SoftwareSerial.h>
/*******************************/
/*Definieren der Ein- /Ausgänge*/
/*******************************/
#define tempSensor   A0
#define lichtSensor  A1
#define led          3
#define tx           6
#define rx           5
/*****************************************/
/*Definieren der Variablen zur Berechnung*/
/*****************************************/
#define mVcc         5000.0       //Vcc in Millivolt
#define Aufloesung   1024.0       //ADC Auflösung
#define mVCelcius    10.0         //mV pro Celcius
#define mVLux        0.33         //mV pro Lux

bool ledStatus       = false;
SoftwareSerial mySerial(rx,tx);

void setup() {
  pinMode(led,OUTPUT);    //Festlegen der Funktion des Pins:
  pinMode(rx,INPUT);      //-lesen oder schreiben
  pinMode(tx,OUTPUT);     
  mySerial.begin(9600);   //Starten der Seriellen Verbindung
}                         //Baudrate 9600

void loop() {
  int tempValue = analogRead(tempSensor);
  int lichtValue = analogRead(lichtSensor);
  tempValue = calcTemp(tempValue);      
  lichtValue = calcEv(lichtValue);       
  if(mySerial.available()){                //Abfrage ob Daten vorhanden
    String str = mySerial.readString();    //Bytes lesen,ls String zurückgeben
    if(str.startsWith("temp")){            //prüfen ob der empfangene String "temp"enthält
      mySerial.print("Temperatur: ");      
      mySerial.print(tempValue);
      mySerial.println("°C");
    }
    if(str.startsWith("lux")){             //empfangenen String vergleichen
      mySerial.print("Helligkeit: ");
      mySerial.print(lichtValue);
      mySerial.println(" Lux");
    }
    if(str.startsWith("led")){             //toggle led über Befehl "led" 
      if(ledStatus == false){
        digitalWrite(led,HIGH);
        ledStatus = true;
      }
      else if(ledStatus == true){
        digitalWrite(led,LOW);
        ledStatus = false;
      }
    }
  }
}

/**********************************************/
/*         Berechnung der Temperatur          */
/*@param analogValue Analogpinwert des Sensors*/
/*@return berechneter Temperaturwert in °C    */
/**********************************************/
int calcTemp(int analogValue){                      
  int sensSpannung = (mVcc*analogValue)/Aufloesung; // A.Wert in Spannung umrechnen
  int temp = sensSpannung/mVCelcius;                // Sensorspannung in Temperatur
  return temp;                                      // umrechnen
}
/**********************************************/
/*    Berechnung der Beleuchtungsstärke       */
/*@param analogValue Analogpinwert des Sensors*/
/*@return berechnete Beleuchtungsstärke in Lux*/
/**********************************************/
int calcEv(int analogValue){                        
  long sensSpannung = (mVcc/Aufloesung)*analogValue;// ADC-Wert in Spannung umrechnen
  long lux = sensSpannung/mVLux;                    // Sensorspannung in Lux umrechnen
  return lux;
}
